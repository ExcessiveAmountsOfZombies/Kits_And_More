plugins {
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle'
    id 'org.spongepowered.mixin'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

apply plugin: 'org.spongepowered.mixin'

version = project.modVersion
group = project.mavenGroup
archivesBaseName = modBaseArchiveName + "-" + minecraftVersion + "-FORGE"

repositories {
    maven {
        name = "Forge"
        url = "https://maven.neoforged.net/releases"
    }
}

configurations {
    shade
    compileOnly.extendsFrom shade
    compileOnly.extendsFrom shadeSource
}

mixin {
    add sourceSets.main, "${project.modId}.refmap.json"
    //config "${project.modId}.mixins.json"
    config "${project.modId}-root.mixins.json"
}

sourceSets {
    main {
        java {
            srcDirs += [
                    rootProject.sourceSets.main.java
            ]
        }
        resources {
            srcDirs += [
                    "src/main/generated",
                    rootProject.sourceSets.main.resources
            ]
        }
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include("META-INF/mods.toml", "pack.mcmeta")
        expand project.properties
    }
}

minecraft {
    //accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    mappings channel: 'official', version: "${project.minecraftVersion}"
    copyIdeResources = true
    //mappings channel: 'parchment', version: "${project.parchmentVersion}-${project.minecraftVersion}"
    runs {
        client {
            workingDirectory project.file('run')
            property 'legacyClassPath', '{minecraft_classpath}'
            arg "-mixin.config=kits_and_more-root.mixins.json"
            // Recommended logging data for a userdev environment
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console //property 'forge.logging.console.level', 'debug'
            args '--mod', "${modId}", '--all', '--output', file('src/generated/resources/'), '--existing', rootProject.file('src/main/resources/')
            mods {
                "${modId}" {
                    sources(sourceSets.main, rootProject.sourceSets.main)
                }
            }
        }

        server {
            workingDirectory project.file('run')
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            mods {
                "${modId}" {
                    sources(sourceSets.main, rootProject.sourceSets.main)
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'legacyClassPath', '{minecraft_classpath}'
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', "${modId}", '--all', '--output', file('src/generated/resources/'), '--existing', rootProject.file('src/main/resources/')
            mods {
                "${modId}" {
                    sources(sourceSets.main, rootProject.sourceSets.main)
                }
            }
        }
    }
}

jarJar.enable()

dependencies {
    minecraft "net.minecraftforge:forge:${minecraftVersion}-${forgeVersion}"
    implementation(rootProject)
    shade(rootProject)
    annotationProcessor "org.spongepowered:mixin:${mixinVersion}:processor"
    implementation (fg.deobf("com.epherical.epherolib:EpheroLib-forge:${project.epherolibVersion}"))

    jarJar(group: 'com.epherical.octo-economy-api', name: 'OctoEconomyApi-forge', version: '[1.1.0-1.18.2]') {
        transitive = false
    }

    implementation fg.deobf("com.epherical.octo-economy-api:OctoEconomyApi-forge:1.1.0-1.18.2")
}

shadowJar {
    archiveClassifier = ""
    enabled = true
    configurations = [project.configurations.shade]
}

artifacts {
    archives shadowJar
    shadow shadowJar
}

jar {
    manifest {
        attributes([
                "Specification-Title": "${modName}",
                "Specification-Vendor": "epherical.com",
                "Specification-Version": "1",
                "Implementation-Title": "${modBaseArchiveName}",
                "Implementation-Version": "${modVersion}",
                "Implementation-Vendor" :"epherical.com",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    source(rootProject.sourceSets.main.allSource)
}

tasks.processResources.dependsOn(rootProject.tasks.processResources)
tasks.build.dependsOn(tasks.shadowJar)

reobf {
    shadowJar {}
}

jar.finalizedBy('reobfJar')
shadowJar.dependsOn('reobfJar')

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "${modBaseArchiveName}-" + artifactId
            version = version + "-${minecraftVersion}"
            // add all the jars that should be included when publishing to maven
            from components.java

            artifact sourceJar
        }
    }

    repositories {
        maven {
            name = "EphericalMaven"
            url = "https://maven.epherical.com/releases"
            credentials {
                username = m_user
                password = m_pass
            }
        }
    }
}
